
raw `
#define LID_BRENDAN 6
#define LID_MAY 7
#define LID_ALEX 14
#define LID_JOEY 12
#define LID_IRENE 8
#define LID_WALLY 9
#define LID_JAMES 10
#define LID_GHOST 5
`

script TrickHouseExt_IntroCutscene_2019 {
	lockall
	delay (30)
	setobjectxyperm (4, 0, 0) //remove the cycling road biker
	addobject (LID_JAMES)
	addobject (LID_GHOST)
	addobject (LID_BRENDAN)
	addobject (LID_MAY)
	addobject (LID_WALLY)
	addobject (LID_IRENE)
	applymovement (LID_JAMES, TrickHouseExt_IntroCutscene_Movement1_Link)
	applymovement (LID_GHOST, TrickHouseExt_IntroCutscene_Movement1a_Ghost)
	applymovement (LID_BRENDAN, TrickHouseExt_IntroCutscene_Movement1_Boy)
	applymovement (LID_MAY, TrickHouseExt_IntroCutscene_Movement1_Girl)
	applymovement (LID_WALLY, TrickHouseExt_IntroCutscene_Movement1_Wally)
	applymovement (LID_IRENE, TrickHouseExt_IntroCutscene_Movement1_Fairy)
	clearflag (FLAG_DISABLE_FADE_INIT)
	fadescreenspeed (0, 6)
	waitmovement (LID_GHOST)
	// Joey is hot
	applymovement (LID_GHOST, TrickHouseExt_IntroCutscene_Movement1b_Ghost)
	message ("{JOEY}: Hey, guys? Hold up a second.\n"
			"I'm melting under this thingâ€¦$")
	waitmovement (LID_JAMES)
	waitmessage
	waitbuttonpress
	playse (SE_SELECT)
	// Link tells him off as he takes off the costume
	applymovement (LID_JAMES, Common_Movement_WalkUp1)
	message ("{LINK}: We can't dilly-dally!\p"
			"We don't have long before Trick-or-\n"
			"Treating time is over!\p"
			"There's loads of houses to hit up in\n"
			"SLATEPORT still!$")
	waitmovement (LID_JAMES)
	delay (16)
	applymovement (LID_GHOST, Common_Movement_WalkInPlaceUp)
	delay (30)
	applymovement (LID_WALLY, Common_Movement_WalkInPlaceLeft)
	setobjectxyperm (LID_JOEY, 39, 33)
	setobjectxyperm (LID_GHOST, 0, 0)
	removeobject (LID_GHOST)
	addobject (LID_JOEY)
	delay (20)
	applymovement (LID_JOEY, Common_Movement_WalkInPlaceDown)
	waitmessage
	waitbuttonpress
	playse (SE_SELECT)
	// Wally notices the house
	applymovement (LID_WALLY, TrickHouseExt_IntroCutscene_Movement2_Wally)
	message ("{WALLY}: We haven't been to this house\n"
			"yet. Let's hit this up first.$")
	waitmovement (LID_WALLY)
	applymovement (LID_BRENDAN, Common_Movement_WalkInPlaceUp)
	delay (3)
	applymovement (LID_JOEY, Common_Movement_WalkInPlaceUp)
	delay (5)
	applymovement (LID_MAY, Common_Movement_WalkInPlaceUp)
	delay (10)
	applymovement (LID_JAMES, TrickHouseExt_IntroCutscene_Movement2_Link)
	waitmessage
	waitbuttonpress
	playse (SE_SELECT)
	// Rival says what it is
	message ("{RIVAL}: That's the TRICK HOUSE.\p"
			"No one lives there anymore since the\n"
			"TRICK MASTER went on his journey.$")
	applymovement (LID_BRENDAN, Common_Movement_WalkInPlaceLeft)
	delay (6)
	applymovement (LID_MAY, Common_Movement_WalkInPlaceLeft)
	delay (4)
	applymovement (LID_JOEY, Common_Movement_WalkInPlaceRight)
	delay (6)
	applymovement (LID_JAMES, Common_Movement_WalkInPlaceRight)
	waitmessage
	waitbuttonpress
	playse (SE_SELECT)
	// Door opens, everyone freaks
	closemessage
	delay (16)
	opendoor (41, 29)
	waitdooranim
	playse (SE_PIN)
	applymovement (LID_JAMES, TrickHouseExt_IntroCutscene_Movement3_What)
	applymovement (LID_JOEY, TrickHouseExt_IntroCutscene_Movement3_What)
	applymovement (LID_BRENDAN, TrickHouseExt_IntroCutscene_Movement3_What)
	applymovement (LID_MAY, TrickHouseExt_IntroCutscene_Movement3_What)
	applymovement (LID_WALLY, TrickHouseExt_IntroCutscene_Movement3_Wally)
	applymovement (LID_IRENE, TrickHouseExt_IntroCutscene_Movement3_What)
	waitmovement (LID_IRENE)
	// 
	applymovement (EVENT_OBJ_ID_PLAYER, TrickHouseExt_IntroCutscene_Movement2_Camera)
	delay (10)
	applymovement (LID_IRENE, TrickHouseExt_IntroCutscene_Movement4_Fairy)
	msgbox ("{FAIRY}: Uh, hello?$")
	//
	closemessage
	waitmovement (LID_IRENE)
	applymovement (LID_IRENE, TrickHouseExt_IntroCutscene_Movement5_Fairy)
	waitmovement (LID_IRENE)
	applymovement (LID_WALLY, TrickHouseExt_IntroCutscene_Movement5_Wally)
	message("{WALLY}: {FAIRY}! Don't go in there!!\n"
			"What if it's haunted?!$")
	waitmovement (LID_WALLY)
	applymovement (LID_IRENE, Common_Movement_WalkInPlaceDown)
	applymovement (LID_JOEY, TrickHouseExt_IntroCutscene_Movement5_Joey)
	delay (12)
	applymovement (LID_MAY, TrickHouseExt_IntroCutscene_Movement5_Girl)
	delay (10)
	applymovement (LID_JAMES, TrickHouseExt_IntroCutscene_Movement5_Link)
	waitmessage
	waitbuttonpress
	playse (SE_SELECT)
	// 
	msgbox ("{FAIRY}: Pfft. I'm not scared.\n"
			"I have my {POKEMON} with me.$")
	applymovement (LID_IRENE, TrickHouseExt_IntroCutscene_Movement6_Fairy)
	//
	msgbox ("{WALLY}: B-But I don't! W-WAIT!!$")
	closemessage
	applymovement (LID_WALLY, TrickHouseExt_IntroCutscene_Movement6_Wally)
	waitmovement (0)
	//
	delay (20)
	applymovement (LID_JAMES, Common_Movement_WalkInPlaceRight)
	message ("{LINK}: C'mon, maybe they have candy.$")
	applymovement (LID_BRENDAN, Common_Movement_WalkInPlaceLeft)
	delay (6)
	applymovement (LID_MAY, Common_Movement_WalkInPlaceLeft)
	delay (8)
	applymovement (LID_JOEY, Common_Movement_WalkInPlaceRight)
	waitmessage
	waitbuttonpress
	playse (SE_SELECT)
	//
	closemessage
	applymovement (LID_JAMES, TrickHouseExt_IntroCutscene_Movement6_Link)
	delay (4)
	applymovement (LID_JOEY, TrickHouseExt_IntroCutscene_Movement6_Joey)
	delay (30)
	applymovement (LID_BRENDAN, Common_Movement_WalkInPlaceRight)
	delay (30)
	applymovement (LID_BRENDAN, TrickHouseExt_IntroCutscene_Movement6_Boy)
	delay (5)
	applymovement (LID_MAY, TrickHouseExt_IntroCutscene_Movement6_Girl)
	//
	fadescreenspeed (1, 6)
	setvar (VAR_INTRO_COUNTER, 2)
	warpcustom (MAP_TRICK_HOUSE_ENTRANCE, 0, 0xFF, 0xFF)
	waitstate
	releaseall
}

movement TrickHouseExt_IntroCutscene_Movement1_Link {
	walk_left * 10
	walk_down * 2
}
movement TrickHouseExt_IntroCutscene_Movement1a_Ghost {
	delay_16 * 5
	walk_left * 6
}
movement TrickHouseExt_IntroCutscene_Movement1b_Ghost {
	walk_left * 4
	walk_in_place_down
}
movement TrickHouseExt_IntroCutscene_Movement1_Wally {
	delay_16 * 4
	walk_left * 7
	delay_16 * 3
	walk_up
	walk_in_place_left
}
movement TrickHouseExt_IntroCutscene_Movement1_Boy {
	delay_16 * 3
	walk_left * 7
}
movement TrickHouseExt_IntroCutscene_Movement1_Girl {
	delay_16 * 1
	walk_left * 8
}
movement TrickHouseExt_IntroCutscene_Movement1_Fairy {
	delay_16 * 2
	walk_left * 11
	walk_in_place_right
}

movement TrickHouseExt_IntroCutscene_Movement2_Wally {
	walk_left
	walk_up * 2
	walk_in_place_down
}
movement TrickHouseExt_IntroCutscene_Movement2_Link {
	walk_right
	walk_in_place_up
}

movement TrickHouseExt_IntroCutscene_Movement2_Camera {
	walk_up * 2
}
movement TrickHouseExt_IntroCutscene_Movement3_Wally {
	walk_in_place_fastest_up
	emote_exclamation_mark
	lock_facing_direction
	jump_down
	unlock_facing_direction
}
movement TrickHouseExt_IntroCutscene_Movement3_What {
	walk_in_place_fastest_up
	emote_exclamation_mark
	delay_16 * 3
}

movement TrickHouseExt_IntroCutscene_Movement4_Fairy {
	walk_right * 2
	walk_up
}

movement TrickHouseExt_IntroCutscene_Movement5_Fairy {
	walk_right
	walk_up
}
movement TrickHouseExt_IntroCutscene_Movement5_Wally {
	emote_exclamation_mark
	walk_fast_up
}
movement TrickHouseExt_IntroCutscene_Movement5_Girl {
	walk_right * 2
	walk_up * 2
}
movement TrickHouseExt_IntroCutscene_Movement5_Link {
	walk_up * 2
}
movement TrickHouseExt_IntroCutscene_Movement5_Joey {
	walk_up
}

movement TrickHouseExt_IntroCutscene_Movement6_Fairy {
	walk_up
	set_invisible
}
movement TrickHouseExt_IntroCutscene_Movement6_Wally {
	walk_fast_up * 2
	set_invisible
}
movement TrickHouseExt_IntroCutscene_Movement6_Link {
	walk_right
	walk_up * 3
	set_invisible
}
movement TrickHouseExt_IntroCutscene_Movement6_Joey {
	walk_right * 2
	walk_up * 3
	set_invisible
}
movement TrickHouseExt_IntroCutscene_Movement6_Boy {
	walk_left
	walk_up * 3
	set_invisible
}
movement TrickHouseExt_IntroCutscene_Movement6_Girl {
	walk_left * 2
	walk_up * 3
	set_invisible
}

raw `
#undef LID_BRENDAN
#undef LID_MAY
#undef LID_JOEY
#undef LID_IRENE
#undef LID_WALLY
#undef LID_JAMES
`
