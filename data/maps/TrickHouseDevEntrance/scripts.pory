mapscripts TrickHouseDevEntrance_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		setvar (VAR_INTRO_COUNTER, 5)
	}
}

script TrickHouseDevEntrance_EventScript_Red {
	msgbox ("This is the dev room. You can change\n"
			"the current puzzle at the TV.\p"
			"If you didn't get here via the door\n"
			"outside, something went wrong;\l"
			"Please report this to the devs.$", MSGBOX_NPC)
}

script TrickHouseDevEntrance_EventScript_SpecialBall {
	lock
	if (flag(FLAG_SYS_POKEMON_GET) == FALSE) {
		givemon (SPECIES_QUILAVA, 50, 0, 0, 0, 0)
		givemon (SPECIES_TROPIUS, 50, 0, 0, 0, 0)
		givemon (SPECIES_LANTURN, 50, 0, 0, 0, 0)
		
		// Quilava Moveset
		setmonmove(0, 1, 150) // Splash
		
		giveitem (ITEM_ESCAPE_ROPE, 3)
		giveitem (ITEM_ITEMFINDER, 1)
		giveitem (ITEM_MAX_REPEL, 99)
		setflag (FLAG_SYS_POKEMON_GET)
		msgbox ("Got Tustin's Debug Set!$")
	}
	release
}

script TrickHouseDevEntrance_EventScript_Pokeball {
	lock
	if (flag(FLAG_SYS_POKEMON_GET) == FALSE) {
		givemon (SPECIES_GROVYLE, 30, 0, 0, 0, 0)
		givemon (SPECIES_COMBUSKEN, 30, 0, 0, 0, 0)
		givemon (SPECIES_MARSHTOMP, 30, 0, 0, 0, 0)
		
		#Grovyle Moveset
		setmonmove(0, 0, 348) # Leaf Blade
		setmonmove(0, 1, 157) # Rock Slide
		setmonmove(0, 2, 280) # Brick Break
		setmonmove(0, 3, 210) # Fury Cutter

		#Combusken Moveset
		setmonmove(1, 0, 53)  # Flamethrower
		setmonmove(1, 1, 24)  # Double Kick 
		setmonmove(1, 2, 332) # Aerial Ace
		setmonmove(1, 3, 339) # Bulk Up

		#Marshtomp Moveset
		setmonmove(2, 0, 352) # Water Pulse
		setmonmove(2, 1, 231) # Iron Tail
		setmonmove(2, 2, 89)  # Earthquake
		setmonmove(2, 3, 341) # Mud Shot


		giveitem (ITEM_ESCAPE_ROPE, 3)
		setflag (FLAG_SYS_POKEMON_GET)
		msgbox ("Got Birch's Starter Set!$")
	}
	release
}

script TrickHouseDevEntrance_EventScript_SoundTest {
	lockall
	callnative (DoSoundTest)
	playse (SE_SELECT)
	message ("Sound Test: {START_BUTTON} Exit$")
	waitstate //script pauses here until manually reenabled
	closemessage
	releaseall
}

script PuzzleDebug_EventScript_PlayCredits {
	lock
	msgbox ("Play credits?$", MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		callnative (PlayCredits)
	}
	release
}

// ----------------------------------------------------
script PuzzleDebug_EventScript_TustinsMenu {
	lock
	faceplayer
	msgbox ("Tustin2121: What do you need to change?$")
	closemessage
	multichoice(0, 0, 115, FALSE)
	switch (var(VAR_RESULT)) {
		case 127: // Cancel
			release
			end
		case 0: // Puzzle Completed
			msgbox("Set number of puzzle completions$")
			loadbyte(1, GAME_STAT_NUM_PUZZLES_COMPLETED)
			callnative(DebugGetGameStat)
			loadword(1, 9999)
			callnative(ChooseNumber)
			waitstate
			if (var(VAR_RESULT) != 0xFFFF) {
				loadhalf(0, VAR_RESULT)
				loadbyte(1, GAME_STAT_NUM_PUZZLES_COMPLETED)
				callnative(DebugSetGameStat)
			}
			release
			end
		case 1: // Puzzle Completed
			msgbox("Set offset of the sun$")
			callnative(DebugGetSunOffset)
			loadword(1, 24*6*256)
			callnative(ChooseNumber)
			waitstate
			if (var(VAR_RESULT) != 0xFFFF) {
				loadhalf(0, VAR_RESULT)
				callnative(DebugSetSunOffset)
			}
			release
			end
	}
	release
}

// Menu defined as MultichoiceList_TustinsMenu in script_menu.c
text(global) PuzzleDebug_TustinsMenu_PuzzlesCompleted { "PuzzCompleted" }
text(global) PuzzleDebug_TustinsMenu_SunOffset { "Sun Offset" }

