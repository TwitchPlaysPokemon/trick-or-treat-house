mapscripts TrickHouseRestroom_MapScripts {
	MAP_SCRIPT_ON_LOAD {
		callnative (GetNumPuzzlesCompleted)
		copyvar(VAR_0x800B, VAR_RESULT)
		
		if (!flag(FLAG_SYS_POKEMON_GET)) {
			// Position the breeder for cutscene
			setobjectxyperm(4, 8, 5)
			setvar(VAR_TEMP_A, 1)
		} else if (var(VAR_0x800B) == 0) {
			setvar(VAR_TEMP_A, 3)
		} else {
			// Hide all pokeballs
			setflag(FLAG_TEMP_1)
			setflag(FLAG_TEMP_2)
			setflag(FLAG_TEMP_3)
			setflag(FLAG_TEMP_4)
			setflag(FLAG_TEMP_5)
			setflag(FLAG_TEMP_6)
			setflag(FLAG_TEMP_7)
			setflag(FLAG_TEMP_8)
			setflag(FLAG_TEMP_9)
			setflag(FLAG_TEMP_A)
			setflag(FLAG_TEMP_B)
			setflag(FLAG_TEMP_C)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_TEMP_A, 1: TrickHouseRestroom_EventScript_IntroCutscene
	]
}

script TrickHouseRestroom_EventScript_IntroCutscene {
	lockall
	setvar(VAR_TEMP_A, 2)
	// Walk in
	applymovement(EVENT_OBJ_ID_PLAYER, TrickHouseRestroom_Movement_IntroPlayer1)
	waitmovement(EVENT_OBJ_ID_PLAYER)
	message("This room is called the Den.\p"
			"You can come here to heal your party,\n"
			"teach them moves, or change their names.\p$")
	waitmessage
	// Turn
	applymovement(EVENT_OBJ_ID_PLAYER, Common_Movement_WalkInPlaceRight)
	applymovement(17, Common_Movement_WalkInPlaceRight)
	message("This young man will get you some\n"
			"{POKEMON} you can borrow for tonight.\p"
			"If you fall in love with one, he'll be\n"
			"here afterwards with adoption papers.\p$")
	waitmessage
	// Turn back
	applymovement(EVENT_OBJ_ID_PLAYER, Common_Movement_WalkInPlaceDown)
	applymovement(17, Common_Movement_WalkInPlaceDown)
	msgbox ("I'll be waiting outside.$")
	closemessage
	// Walk inside
	applymovement(4, TrickHouseRestroom_Movement_IntroBreeder2)
	applymovement(17, TrickHouseRestroom_Movement_IntroLady2)
	applymovement(EVENT_OBJ_ID_PLAYER, TrickHouseRestroom_Movement_IntroPlayer2)
	waitmovement(4)
	//
	msgbox ("Here's a selection of my buddies looking\n"
			"for a good home.\p"
			"See if any three of them strike\n"
			"your fancy. I'll be waiting over here.$")
	closemessage
	//
	applymovement(4, TrickHouseRestroom_Movement_IntroBreeder3)
	waitmovement(4)
	setobjectxyperm(4, 10, 3)
	setobjectmovementtype(4, MOVEMENT_TYPE_FACE_RIGHT)
	releaseall
}

movement TrickHouseRestroom_Movement_IntroPlayer1 {
	walk_up * 3
}
movement TrickHouseRestroom_Movement_IntroBreeder2 {
	delay_16 * 3
	walk_right
	walk_up * 2
	walk_right * 7
	walk_in_place_fastest_down
}
movement TrickHouseRestroom_Movement_IntroPlayer2 {
	walk_right * 4
	walk_up * 2
	walk_right * 7
	walk_in_place_fastest_down
}
movement TrickHouseRestroom_Movement_IntroLady2 {
	delay_16
	walk_in_place_fastest_right
	delay_16 * 2
	walk_down * 4
	set_invisible
}

movement TrickHouseRestroom_Movement_IntroBreeder3 {
	walk_up
	walk_left * 2
	walk_down
	walk_left * 4
	walk_in_place_fastest_right
}

// ----------------------------------------------------

script(local) TrickHouseRestroom_EventScript_Breeder_PartyBuild {
	// Currently getting starting party
	lock
	getpartysize
	switch (var(VAR_RESULT)) {
		case 0:
			msgbox ("What's wrong? Don't any of my buddies\n"
					"appeal to you?\p"
					"You'll need to choose three to do\n"
					"the Trick House puzzles. You don't\l"
					"have to keep them after.$")
			release
			end
		case 1:
			msgbox ("How are you doing? You still need\n"
					"two more {POKEMON} to do the puzzles.$")
			release
			end
		case 2:
			msgbox ("How are you doing?\n"
					"You still need one more {POKEMON}.$")
			release
			end
	}
	msgbox ("How are you doing?\p"
			"Oh, you've got a party now! Excellent!\p"
			"Good luck with the puzzles!$")
	setvar(VAR_TEMP_A, 3)
	// Move the player out
	applymovement (4, TrickHouseRestroom_Movement_IntroBreeder4)
	applymovement (EVENT_OBJ_ID_PLAYER, TrickHouseRestroom_Movement_IntroPlayer4)
	waitmovement (4)
	release
}

movement TrickHouseRestroom_Movement_IntroBreeder4 {
	walk_left
	walk_up
	delay_16 * 2
	walk_down
	walk_right
	walk_in_place_fastest_left
}
movement TrickHouseRestroom_Movement_IntroPlayer4 {
	walk_left * 2
	walk_down
}


// ----------------------------------------------------
// Counter people

script TrickHouseRestroom_EventScript_Healer {
	
}

script TrickHouseRestroom_EventScript_Relearner {
	msgbox ("I'm the MOVE REMINDER.\p"
			"I can teach moves that your {POKEMON}\n"
			"may have forgotten.\p"
			"Would you like to teach one of your\n"
			"{POKEMON} a forgotten move?$")
	yesnobox (20, 8)
	if (var(VAR_RESULT) == NO) {
		msgbox ("Good luck on the puzzles.$")
		end
	}
	special (ChooseMonForMoveRelearner)
	waitstate
	if (var(VAR_0x8004) == 255) {
		msgbox ("Good luck on the puzzles.$")
		end
	}
	
}

script TrickHouseRestroom_EventScript_Renamer {
	
}

// ----------------------------------------------------

script TrickHouseRestroom_EventScript_Breeder {
	if (var(VAR_TEMP_A) == 2) {
		goto (TrickHouseRestroom_EventScript_Breeder_PartyBuild)
	}
	if (var(VAR_TEMP_A) == 3) {
		msgbox ("Hey, back to look at more? Don't blame\n"
				"you: they're pretty cute, aren't they?\p"
				"But you've got three with you already.\n"
				"The Trick Master's rules say only three.$")
	}
}

script TrickHouseRestroom_EventScript_Pokeball {
	lockall
	getpartysize
	if (var(VAR_RESULT) >= 3) {
		msgbox ("You only need three {POKEMON}. You\n"
				"should leave some for other people.$")
		releaseall
		end
	}
	
	copyvar(VAR_0x8000, VAR_LAST_TALKED) // Use the local event id to index which starters
	subvar(VAR_0x8000, 5)
	
	do {
		// Sets VAR_0x8001 to the selected mon species, also buffers mon name to STR_VAR_1
		callnative(GetOrGenerateStarterPokemon) 
		bufferspeciesname(1, VAR_0x8001) //buffer 2
		drawmonpic (VAR_0x8001, 8, 4)
		message("The name tag on this {STR_VAR_2} says\n"
				"“{STR_VAR_1}”.")
		waitmessage
		multichoice(20, 8, 114, 2)
		switch (var(VAR_RESULT)) {
			case 0: // choose
				erasemonpic
				goto (TrickHouseRestroom_EventScript_PokemonChoose)
				end
			case 1: // Status screen
				erasemonpic
				
			case 2: // Cancel
				break
		}
	} while (var(VAR_RESULT) != 127)
	erasemonpic
	releaseall
}

script(local) TrickHouseRestroom_EventScript_PokemonChoose {
	message("You chose {STR_VAR_1} for your party!$")
	playfanfare (MUS_FANFA4)
	removeobject (VAR_LAST_TALKED)
	callnative (GivePlayerMonFromBox)
	waitmessage
	waitfanfare
	//
	message("Would you like to give {STR_VAR_1} a\n"
			"new nickname?$")
	waitmessage
	yesnobox(20, 8)
	if (var(VAR_RESULT) == YES) {
		getpartysize
		copyvar (VAR_0x8004, VAR_RESULT)
		subvar (VAR_0x8004, 1)
		call (Common_EventScript_NameReceivedPokemon)
	}
	setflag (FLAG_SYS_POKEMON_GET)
	releaseall
}


text(global) PokemonAdoption_Choose { "Choose" }
text(global) PokemonAdoption_Status { "Stats" }
text(global) PokemonAdoption_Exit   { "Cancel" }
