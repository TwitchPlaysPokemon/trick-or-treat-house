raw `
#define VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS VAR_PUZZLE_00

Puzzle_SpotDifference_2_Right_MapScripts::
    puzzle_info Puzzle_SpotDifference_Name, Puzzle_SpotDifference_Author
    map_script MAP_SCRIPT_ON_FRAME_TABLE, Puzzle_SpotDifference_2_Right_FrameTable
    .byte 0

Puzzle_SpotDifference_2_Right_FrameTable:
    map_script_2 VAR_TEMP_1, 0, Puzzle_SpotDifference_2_Right_DoSparkles
    .2byte 0
`

script Puzzle_SpotDifference_2_Right_DoSparkles {
    setvar(VAR_TEMP_1, 1)
    if (var(VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS) > 3) {
        return
    }

    special(SpawnCameraObject)
    applymovement(EVENT_OBJ_ID_CAMERA, Puzzle_SpotDifference_2_Movement_CameraPanUp)
    waitmovement(0)
    special(RemoveCameraObject)
    delay(20)
    setfieldeffectargument(0, 5)
    setfieldeffectargument(1, 8)
    setfieldeffectargument(2, 0)
    dofieldeffect(FLDEFF_SPARKLE)
    setfieldeffectargument(0, 10)
    setfieldeffectargument(1, 3)
    setfieldeffectargument(2, 0)
    dofieldeffect(FLDEFF_SPARKLE)
    setfieldeffectargument(0, 14)
    setfieldeffectargument(1, 10)
    setfieldeffectargument(2, 0)
    dofieldeffect(FLDEFF_SPARKLE)
    setfieldeffectargument(0, 4)
    setfieldeffectargument(1, 4)
    setfieldeffectargument(2, 0)
    dofieldeffect(FLDEFF_SPARKLE)
    waitfieldeffect(FLDEFF_SPARKLE)
    delay(10)
    special(SpawnCameraObject)
    applymovement(EVENT_OBJ_ID_CAMERA, Puzzle_SpotDifference_2_Movement_CameraPanDown)
    waitmovement(0)
    special(RemoveCameraObject)
}

script Puzzle_SpotDifference_2_Right_Trigger {
    lock
    if (var(VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS) > 3) {
        # Player already found this trigger.
        msgbox("There's nothing here.$")
    } else {
        setvar(VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS, 4)
        playfanfare(MUS_ME_B_SMALL)
        msgbox("CORRECT!\n"
               "{PLAYER} found the different sparkle!$")
        waitfanfare
        closemessage
        playse(SE_DOOR)
        waitse
        delay(30)
        msgbox("It sounds like a door opened in\n"
               "another room.$")
    }
    release
}

script Puzzle_SpotTheDifference_Trainer4 {
    trainerbattle_single(TRAINER_04, Puzzle_SpotTheDifference_Trainer4_Intro, Puzzle_SpotTheDifference_Trainer4_Defeat)
    msgbox(Puzzle_SpotTheDifference_Trainer4_After, MSGBOX_AUTOCLOSE)
}

text Puzzle_SpotTheDifference_Trainer4_Intro {
    "Be mine!"
}

text Puzzle_SpotTheDifference_Trainer4_Defeat {
    "I want a divorce!"
}

text Puzzle_SpotTheDifference_Trainer4_After {
    "Do you know any single zombies in\n"
    "the area?\p"
    "I could use a wingman."
}

