raw `
#define VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS VAR_PUZZLE_00

Puzzle_SpotDifference_2_Left_MapScripts::
    map_script MAP_SCRIPT_ON_LOAD, Puzzle_SpotDifference_2_Left_OnLoad
    map_script MAP_SCRIPT_ON_FRAME_TABLE, Puzzle_SpotDifference_2_Left_FrameTable
    .byte 0

Puzzle_SpotDifference_2_Left_FrameTable:
    map_script_2 VAR_TEMP_1, 0, Puzzle_SpotDifference_2_Left_DoSparkles
    .2byte 0
`

script Puzzle_SpotDifference_2_Left_OnLoad {
    if (var(VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS) == 2) {
        setvar(VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS, 3)
    }
}

script Puzzle_SpotDifference_2_Left_DoSparkles {
    setvar(VAR_TEMP_1, 1)
    if (var(VAR_SPOT_DIFFERENCE_NUM_UNLOCKED_ROOMS) > 3) {
        return
    }

    special(SpawnCameraObject)
    applymovement(EVENT_OBJ_ID_CAMERA, Puzzle_SpotDifference_2_Movement_CameraPanUp)
    waitmovement(0)
    special(RemoveCameraObject)
    delay(20)
    setfieldeffectargument(0, 5)
    setfieldeffectargument(1, 8)
    setfieldeffectargument(2, 0)
    dofieldeffect(FLDEFF_SPARKLE)
    setfieldeffectargument(0, 10)
    setfieldeffectargument(1, 3)
    setfieldeffectargument(2, 0)
    dofieldeffect(FLDEFF_SPARKLE)
    setfieldeffectargument(0, 14)
    setfieldeffectargument(1, 10)
    setfieldeffectargument(2, 0)
    dofieldeffect(FLDEFF_SPARKLE)
    waitfieldeffect(FLDEFF_SPARKLE)
    delay(10)
    special(SpawnCameraObject)
    applymovement(EVENT_OBJ_ID_CAMERA, Puzzle_SpotDifference_2_Movement_CameraPanDown)
    waitmovement(0)
    special(RemoveCameraObject)
}

movement Puzzle_SpotDifference_2_Movement_CameraPanUp {
    walk_up * 5
}

movement Puzzle_SpotDifference_2_Movement_CameraPanDown {
    walk_down * 5
}

script Puzzle_SpotTheDifference_Trainer3 {
    trainerbattle_single(TRAINER_03, Puzzle_SpotTheDifference_Trainer3_Intro, Puzzle_SpotTheDifference_Trainer3_Defeat)
    msgbox(Puzzle_SpotTheDifference_Trainer3_After, MSGBOX_AUTOCLOSE)
}

text Puzzle_SpotTheDifference_Trainer3_Intro {
    "La-lalala!\p"
    "You fell for my siren song!"
}

text Puzzle_SpotTheDifference_Trainer3_Defeat {
    "Whoops, I fell over!"
}

text Puzzle_SpotTheDifference_Trainer3_After {
    "I want to be where the people are.\n"
    "Not in this gross trash can."
}
