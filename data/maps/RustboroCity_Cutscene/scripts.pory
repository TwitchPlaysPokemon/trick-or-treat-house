mapscripts RustboroCity_Cutscene_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_INTRO_COUNTER, 0: RustboroCity_PanHalloween
	]
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_INTRO_COUNTER, 0 {
			setflag (FLAG_DISABLE_FADE_INIT)
			callnative (HidePlayerOnLoad)
		}
	]
}


script(local) RustboroCity_PanHalloween {
	lockall
	delay (60)
	applymovement (EVENT_OBJ_ID_PLAYER, RustboroCity_CameraWalk)
	clearflag (FLAG_DISABLE_FADE_INIT)
	fadescreenspeed (0, 6)
	// Fade up screen, pan across city
	applymovement (5, RustboroCity_Movement_WalkAwayBoy)
	applymovement (2, Common_Movement_WalkInPlaceLeft)
	delay (20)
	applymovement (9, RustboroCity_Movement_Hop2)
	delay (30)
	delay (40)
	applymovement (6, RustboroCity_Movement_Sidestep1)
	delay (4)
	applymovement (8, RustboroCity_Movement_Sidestep2)
	delay (10)
	applymovement (2, Common_Movement_WalkInPlaceRight)
	delay (10)
	setdooropen (26, 34)
	applymovement (3, RustboroCity_Movement_WalkDown13)
	applymovement (4, RustboroCity_Movement_WalkDown13)
	// May going to next house
	delay (30)
	applymovement (7, Common_Movement_WalkInPlaceDown)
	closedoor (26, 34)
	waitdooranim
	applymovement (7, RustboroCity_Movement_WalkNextGirl)
	waitmovement (7)
	delay (10)
	opendoor (30, 28)
	waitdooranim
	applymovement (13, Common_Movement_SetVisible)
	waitse
	delay (8)
	playse (SE_DANSA)
	applymovement (7, RustboroCity_Movement_SurpriseJump)
	// 
	waitmovement (EVENT_OBJ_ID_PLAYER)
	// Fade down screen
	applymovement (EVENT_OBJ_ID_PLAYER, RustboroCity_CameraWalkFinish)
	fadescreenspeed (1, 6)
	warpcustom (MAP_MAUVILLE_CITY_CUTSCENE, 0, -1, -1)
	waitstate
	releaseall
}

movement RustboroCity_CameraWalk {
	walk_slow_diag_northeast * 16
}
movement RustboroCity_CameraWalkFinish {
	walk_slow_diag_northeast * 3
}
movement RustboroCity_Movement_WalkAwayBoy {
	walk_right * 5
	walk_down * 3
}
movement RustboroCity_Movement_WalkNextGirl {
	walk_down
	walk_right * 4
	walk_up * 7
}
movement RustboroCity_Movement_SurpriseJump {
	emote_exclamation_mark
	lock_facing_direction
	jump_down
	unlock_facing_direction
}
movement RustboroCity_Movement_Hop2 {
	jump_in_place_right * 2
}
movement RustboroCity_Movement_WalkDown13 {
	walk_fast_down * 13
}
movement RustboroCity_Movement_Sidestep1 {
	walk_down * 2
	walk_right
}
movement RustboroCity_Movement_Sidestep2 {
	walk_right
	walk_down * 2
	walk_in_place_fast_left
}

